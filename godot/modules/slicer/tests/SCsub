#!/usr/bin/env python

import glob
import subprocess
import sys

Import('env')

# A simple SCons Action that runs the freshly built target
# (It's very likely SCons already has something like this but
# I couldn't find it)
def run_target(target, source, env):
    return subprocess.call(target[0].abspath)

# Our unit tests make use of the OS_Server class (which is unfortunately Unix only)
# for bootstraping the Godot environment before running tests. This is essentially
# just copy-and-pasted from `platform/server/SCsub` and for that reason runs a high
# risk of falling out of sync.
def os_server_sources():
    common_server = [\
        "#platform/server/os_server.cpp",\
    ]

    if sys.platform == "darwin":
        common_server.append("#platform/osx/crash_handler_osx.mm")
        common_server.append("#platform/osx/power_osx.cpp")
        common_server.append("#platform/osx/semaphore_osx.cpp")
    else:
        common_server.append("#platform/x11/crash_handler_x11.cpp")
        common_server.append("#platform/x11/power_x11.cpp")

    # If you look in `drivers/SCsub` you'll see that these dummy drivers are
    # only compiled if you're specifically building against the `server` platform.
    # We still want users to be able to build against their regular hostmachine
    # platform (osx, x11) rather than have to remember that minutia so we just
    # throw in those dummy drivers ourselves.
    common_server += glob.glob('../../../drivers/dummy/**/*.cpp', recursive=True)

    return common_server

# With the exception of a few one off test programs, Godot does not currently have
# much in the way of unit testing the engine code itself and this might be an
# interesting opportunity to see what that process might look like, for those it behooves.
#
# We use Catch2 (https://github.com/catchorg/Catch2) as our test framework.
# This function simply configures a new executable build for a binary that
# will run our test suite.
def configure_unit_tests():
    # Source files that are necessary for the actual executable binary
    test_main_files = ['main.cpp'] + os_server_sources()

    # Source files of our tests suite
    test_files = glob.glob('./**/*.test.cpp', recursive=True)

    test_program = None

    # As tests usually need to be modified and tweaked iteratively, a quick
    # build phase is hugely beneficial. But, as mentioned before,
    # the linking all of Godot's main sources and libraries can take a long time
    # even if only one or two files are actually being compiled. To help mitigate
    # this let's try to separate the building of the Catch2 main function
    # (which should not be touched very often) and the building of the test suite
    # itself (which we imagine _will_ be changing often).
    if ARGUMENTS.get('slicer_shared', 'no') == 'yes':
        test_env = env.Clone()
        test_env.Append(CCFLAGS=['-fPIC'])
        test_env['LIBS'] = []
        test_lib = test_env.SharedLibrary(target='#bin/slicer-test-suite', source=test_files)
        test_env.Alias('slicer-test-shared', [test_lib])

        # Catch2's test auto registering functionality doesn't work very well with linked
        # libraries (https://github.com/catchorg/Catch2/issues/421), as the linker will
        # try to strip out what it thinks are unused symbols. So we need to pass in some
        # additional flags to ask the linker to not do that
        if sys.platform == "darwin":
            test_env.Append(LINKFLAGS=['-Wl,-undefined,dynamic_lookup'])
            env.Append(LINKFLAGS=['-Wl,-force_load', test_lib[0].abspath])
        else:
            # This has not been tested on a GNU/linux compiler but this _should_
            # be the equivelant as the above `force_load`
            env.Append(LINKFLAGS=['-Wl,--whole-archive', test_lib[0].abspath, '-Wl,--no-whole-archive'])

        # TODO - Updating the main SCons environment with our test build is not smart. It actually
        # affectively breaks the *real* godot executable build if you build it with `slicer_shared`
        # and `slicer_tests` both set. But the issue is that the `env` environment still has some required
        # libraries that will be added after this SCSub is run (see the SConstruct file). If we clone the
        # environment here (or set the LIBS arg in the env.Program call itself) we won't get those libraries
        # when the program tries to build. Basically, we need to keep the build referenced to the `env` object
        # so that it will have *all* the configuration by the time it builds
        env.Append(LIBS=[test_lib])

        test_program = env.Program('#bin/test-slicer', test_main_files)
    else:
        # As you can tell, there's a bit of finickiness with getting a shared library build
        # working, so we add a more straightforward static build option just in case for those
        # who don't want to deal with it
        test_program = env.Program('#bin/test-slicer', test_main_files + test_files)

    env.Alias('slicer-test', [test_program])

    # Useful so we can more easily attach an IDE's debugger to the build process
    if ARGUMENTS.get('skip_running_tests', 'no') == 'no':
        # Passing in a function to `AddPostAction` is slightly more preferrable
        # to just passing in test[0].abspath, as the latter runs the risk
        # of causing cyclical dependency issues if you start playing around
        # with the `skip_running_tests` flag. Using a function as an
        # intermediary is essentially just a hack to keep SCons from trying
        # to make the test binary a dependency of itself.
        env.AddPostAction(test_program, run_target)

configure_unit_tests()
