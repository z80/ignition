#!/usr/bin/env python

Import('env')

common_haiku = [
    'os_haiku.cpp',
    'context_gl_haiku.cpp',
    'haiku_application.cpp',
    'haiku_direct_window.cpp',
    'haiku_gl_view.cpp',
    'key_mapping_haiku.cpp',
    'audio_driver_media_kit.cpp'
]

target = env.add_program(
    '#bin/godot',
    ['godot_haiku.cpp'] + common_haiku
)

command = env.Command('#bin/godot.rsrc', '#platform/haiku/godot.rdef',
                      ['rc -o $TARGET $SOURCE'])


def addResourcesAction(target=None, source=None, env=None):
    return env.Execute('xres -o ' + File(target)[0].path + ' bin/godot.rsrc')

env.AddPostAction(target, addResourcesAction)
env.Depends(target, command)
